plugins {
    id 'java-library'
    id "io.freefair.lombok" version "6.0.0-m2"
    id 'io.qameta.allure' version '2.12.0'
}

group = 'sandrounknown'
version = '1.0-SNAPSHOT'

def junitVersion = "5.11.3",
    selenideVersion = "7.7.0",
    lowerSelenideVersion = "6.13.0",
    restAssuredVersion = "5.5.0",
    allureVersion = "2.29.0",
    assertjVersion = "3.24.2",
    commonsIoVersion = "2.14.0",
    jacksonVersion = "2.18.2",
    ownerVersion = "1.0.12",
    appiumVersion = "8.3.0"

repositories {
    mavenCentral()
}

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

configurations {
    androidTestImplementation
}

dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "org.assertj:assertj-core:$assertjVersion",
            "commons-io:commons-io:$commonsIoVersion",
            "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "io.appium:java-client:$appiumVersion"
    )

    // Эта зависимость будет добавлена только если идет тест с тегом ANDROID.
    androidTestImplementation "com.codeborne:selenide:$lowerSelenideVersion"

    // Зависимость по умолчанию для всех тестов
    testImplementation "com.codeborne:selenide:$selenideVersion"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task web(type: Test) {
    useJUnitPlatform {
        //includeTags("WEB")
        includeTags("WEB_FOR_TEST")
    }
}

task android(type: Test) {
    // Добавляем зависимость для ANDROID тестов
    classpath += configurations.androidTestImplementation
    useJUnitPlatform {
        //includeTags("ANDROID")
        includeTags("MOBILE_FOR_TEST")
    }
}

tasks.android.dependsOn(test)